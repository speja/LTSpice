* Source https://en-support.renesas.com/knowledgeBase/4966729
* EL7144 Spice Model   
*Revision A, February 1996
*Connection     In+
*               |       In-
*               |       |       Gnd
*               |       |       |       Outn
*               |       |       |       |       outp
*               |       |       |       |       |       Vcc
.subckt EL7144/EL   2       3       5       6       7       8 
V1 12 5 1.6
R1 13 15 1k 
R2 14 15 5k  
R5 11 12 100 
C1 15 5 43.3pF
D1 14 13 dmod
X1 13 11 2 5 comp1
X2 16 12 15 5 comp1
V2 22 5 1.6
R6 23 25 1K
R7 24 25 5K
R8 21 22 100
C2 25 5 43.3pF
D2 24 23 dmod
X3 23 21 3 5 comp1
X4 26 25 22 5 comp1
X5 16 26 17 5 And-gate
sp 8 7 17 5 spmod
sn 6 5 17 5 snmod 
g1 11 5 13 5 938u
g2 21 5 23 5 938u
.model dmod d
.model spmod vswitch ron=3 roff=2meg von=1 voff=1.5
.model snmod vswitch ron=4 roff=2meg von=3 voff=2
.ends EL7144/EL
.subckt And-gate inp1 inp2 out-A Vss-A
e1 out-A Vss-A table {v(inp1)*v(inp2)} = (0,3.2) (3.2,0)
Rout-a out-a vss-a 10meg
rinpa inp1 vss-a 10meg
rinpb inp2 vss-a 10meg
.ends and-gate
.subckt comp1 out inp inm vss
e1 out vss table {(v(inp)-v(inm))*5000} = (0,0) (3.2,3.2)
Rout out vss 10meg
Rinp inp vss 10meg
Rinm inm vss 10meg
.ends comp1
*$
